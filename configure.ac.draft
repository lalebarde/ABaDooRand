#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.
AC_PREREQ(2.61)
#
# The ABaDooRand version number is extracted from include/abadoorand.h using
# the version.sh script. Hence, it should be updated there.
# The version in git should reflect the *next* version planned.
#
AC_INIT([abadoorand],[m4_esyscmd([./version.sh])],[abadoorand@freelists.org])

AC_CONFIG_AUX_DIR(config)
AC_CONFIG_MACRO_DIR(config)
AC_CONFIG_HEADERS([src/platform.hpp])
AM_INIT_AUTOMAKE(tar-ustar dist-zip foreign)
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

# This lets us use PACKAGE_VERSION in Makefiles
AC_SUBST(PACKAGE_VERSION)

# Libtool -version-info (ABI version)
#
# Don't change this unless you know exactly what you're doing and have read and
# understand:
# http://www.gnu.org/software/libtool/manual/html_node/Updating-version-info.html
#
# Changes:
#
# ABaDooRand -version-info current:revision:age
LTVER="0:1:0"
AC_SUBST(LTVER)

# Take a copy of original flags
ABADOORAND_ORIG_CFLAGS="${CFLAGS:-none}"
ABADOORAND_ORIG_CPPFLAGS="${CPPFLAGS:-none}"
ABADOORAND_ORIG_CXXFLAGS="${CXXFLAGS:-none}"

# Checks for programs.
AC_PROG_CC
AC_PROG_CC_C99
AC_PROG_CXX
AM_PROG_CC_C_O
AC_PROG_SED
AC_PROG_AWK

# Libtool configuration for different targets. See acinclude.m4
AC_ARG_VAR([XMLTO], [Path to xmlto command])
AC_PATH_PROG([XMLTO], [xmlto])
AC_ARG_VAR([ASCIIDOC], [Path to asciidoc command])
AC_PATH_PROG([ASCIIDOC], [asciidoc])
LIBABADOORAND_CONFIG_LIBTOOL
AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL

# Check whether to build a with debug symbols
LIBABADOORAND_CHECK_ENABLE_DEBUG

# Check wheter to enable code coverage
LIBABADOORAND_WITH_GCOV

AC_MSG_CHECKING([if TIPC is available and supports nonblocking connect])
AC_RUN_IFELSE([AC_LANG_PROGRAM([[ #include <stdlib.h>
                                  #include <string.h>
                                  #include <fcntl.h>
                                  #include <errno.h>
                                  #include <sys/socket.h>
                                  #include <linux/tipc.h>
			          ]],[[
                                      struct sockaddr_tipc topsrv;
                                      int sd = socket(AF_TIPC, SOCK_SEQPACKET, 0);
                                      if (sd == -EAFNOSUPPORT) {
                                          return 1;
                                      }
                                      memset(&topsrv, 0, sizeof(topsrv));
                                      topsrv.family = AF_TIPC;
                                      topsrv.addrtype = TIPC_ADDR_NAME;
                                      topsrv.addr.name.name.type = TIPC_TOP_SRV;
                                      topsrv.addr.name.name.instance = TIPC_TOP_SRV;
                                      fcntl(sd, F_SETFL, O_NONBLOCK);
                                      if (connect(sd, (struct sockaddr *)&topsrv,
					          sizeof(topsrv)) != 0) {
                                          if (errno != EINPROGRESS)
					  	return -1;
                                      }]])
                                  ],
               [abadoorand_tipc_support=yes],
               [abadoorand_tipc_support=no],
               [abadoorand_tipc_support=no])
AC_MSG_RESULT([$abadoorand_tipc_support])

# Allow libsodium to be installed in a custom path:

ABADOORAND_ORIG_CXXFLAGS="-std=c++11 ${ABADOORAND_ORIG_CPPFLAGS}"
CXXFLAGS="-std=c++11  ${CPPFLAGS}"
CXX=gcc-4.7.3

AC_ARG_WITH([libsodium],
            [AS_HELP_STRING([--with-libsodium],
                            [Specify libsodium prefix])],
            [abadoorand_search_libsodium="yes"],
            [])

if test "x$abadoorand_search_libsodium" = "xyes"; then
    if test -r "${with_libsodium}/include/sodium.h"; then
        CPPFLAGS="-I${with_libsodium}/include ${CPPFLAGS}"
        LDFLAGS="-L${with_libsodium}/lib ${LDFLAGS}"
    fi
fi

AC_ARG_WITH([libsodium-include-dir],
            [AS_HELP_STRING([--with-libsodium-include-dir],
                            [Specify libsodium include prefix])],
            [abadoorand_search_libsodium_include="yes"],
            [])

if test "x$abadoorand_search_libsodium_include" = "xyes"; then
    if test -r "${with_libsodium_include_dir}/sodium.h"; then
        CPPFLAGS="-I${with_libsodium_include_dir}/include ${CPPFLAGS}"
    fi
fi

AC_ARG_WITH([libsodium_lib_dir],
            [AS_HELP_STRING([--with-libsodium-lib-dir],
                            [Specify libsodium library prefix])],
            [abadoorand_search_libsodium_lib="yes"],
            [])

if test "x$abadoorand_search_libsodium_lib" = "xyes"; then
    if test -r "${with_libsodium_lib_dir}/libsodium.{a|so|dylib}"; then
        LDFLAGS="-L${with_libsodium}/lib ${LDFLAGS}"
    fi
fi


AC_ARG_WITH([relaxed],
            [AS_HELP_STRING([--with-relaxed],
                            [Switch off pedantic compiler])],
            [abadoorand_relaxed="yes"],
            [])

if test "x$abadoorand_relaxed" = "xyes"; then
    abadoorand_pedantic="no"
else
    abadoorand_pedantic="yes"
fi

# By default compiling with -Werror except OSX.
abadoorand_werror="yes"

# By default use DSO visibility
abadoorand_dso_visibility="yes"

# Platform specific checks
abadoorand_on_mingw32="no"
abadoorand_on_android="no"
abadoorand_on_linux="no"

# Set some default features required by ABaDooRand code.
CPPFLAGS="-D_REENTRANT -D_THREAD_SAFE $CPPFLAGS"

# For host type checks
AC_CANONICAL_HOST

# OS-specific tests
case "${host_os}" in
    *linux*)
        # Define on Linux to enable all library features. Define if using a gnu compiler
        if test "x$GXX" = "xyes"; then
            CPPFLAGS="-D_GNU_SOURCE $CPPFLAGS"
        fi
        AC_DEFINE(ABADOORAND_HAVE_LINUX, 1, [Have Linux OS])
        abadoorand_on_linux="yes"

        if test "x$abadoorand_tipc_support" = "xyes"; then
            AC_DEFINE(ABADOORAND_HAVE_TIPC, 1, [Have TIPC support])
        fi
        case "${host_os}" in
            *android*)
                AC_DEFINE(ABADOORAND_HAVE_ANDROID, 1, [Have Android OS])
                abadoorand_on_android="yes"
            ;;
        esac
        ;;
    *solaris*)
        # Define on Solaris to enable all library features
        CPPFLAGS="-D_PTHREADS $CPPFLAGS"
        AC_DEFINE(ABADOORAND_HAVE_SOLARIS, 1, [Have Solaris OS])
        AC_CHECK_LIB(socket, socket)
        AC_CHECK_LIB(nsl, gethostbyname)
        AC_MSG_CHECKING([whether atomic operations can be used])
        AC_COMPILE_IFELSE([AC_LANG_PROGRAM(
            [[#include <atomic.h>]],
            [[uint32_t value;
              atomic_cas_32 (&value, 0, 0);
              return 0;]])],
            [solaris_has_atomic=yes],
            [solaris_has_atomic=no])
        AC_MSG_RESULT([$solaris_has_atomic])
        # Solaris 8 does not have atomic operations exported to user space.
        if test "x$solaris_has_atomic" = "xno"; then
            AC_DEFINE(ABADOORAND_FORCE_MUTEXES, 1, [Force to use mutexes])
        fi
        # ssp library is required for libsodium on Solaris-like systems
        LDFLAGS="-lssp $LDFLAGS"
        CPPFLAGS="$CPPFLAGS -Wno-long-long"
        ;;
    *freebsd*)
        # Define on FreeBSD to enable all library features
        CPPFLAGS="-D__BSD_VISIBLE $CPPFLAGS"
        AC_DEFINE(ABADOORAND_HAVE_FREEBSD, 1, [Have FreeBSD OS])
        ;;
    *darwin*)
        # Define on Darwin to enable all library features
        CPPFLAGS="-D_DARWIN_C_SOURCE $CPPFLAGS"
        abadoorand_pedantic="no"
        abadoorand_werror="no"
        AC_DEFINE(ABADOORAND_HAVE_OSX, 1, [Have DarwinOSX OS])
        AC_LANG_PUSH([C++])
        LIBABADOORAND_CHECK_LANG_FLAG_PREPEND([-Wno-uninitialized])
        AC_LANG_POP([C++])
        ;;
    *netbsd*)
        # Define on NetBSD to enable all library features
        CPPFLAGS="-D_NETBSD_SOURCE $CPPFLAGS"
        AC_DEFINE(ABADOORAND_HAVE_NETBSD, 1, [Have NetBSD OS])
        # NetBSD 5.0 and newer provides atomic operations but we can
        # only use these on systems where PR #42842 has been fixed so
        # we must try and link a test program using C++.
        abadoorand_netbsd_has_atomic=no
        AC_MSG_CHECKING([whether atomic operations can be used])
        AC_LANG_PUSH([C++])
        AC_LINK_IFELSE([AC_LANG_PROGRAM(
            [[#include <atomic.h>]],
            [[uint32_t value;
              atomic_cas_32 (&value, 0, 0);
              return 0;]])],
            [abadoorand_netbsd_has_atomic=yes],
            [abadoorand_netbsd_has_atomic=no])
        AC_LANG_POP([C++])
        AC_MSG_RESULT([$abadoorand_netbsd_has_atomic])
        if test "x$abadoorand_netbsd_has_atomic" = "xno"; then
            AC_DEFINE(ABADOORAND_FORCE_MUTEXES, 1, [Force to use mutexes])
        fi
        ;;
    *openbsd*)
        # Define on OpenBSD to enable all library features
        CPPFLAGS="-D_BSD_SOURCE $CPPFLAGS"
        AC_DEFINE(ABADOORAND_HAVE_OPENBSD, 1, [Have OpenBSD OS])
        ;;
    *nto-qnx*)
        abadoorand_pedantic="no"
        AC_DEFINE(ABADOORAND_HAVE_QNXNTO, 1, [Have QNX Neutrino OS])
        AC_CHECK_LIB(socket, socket)
        ;;
    *aix*)
        AC_DEFINE(ABADOORAND_HAVE_AIX, 1, [Have AIX OS])
        ;;
    *hpux*)
        # Define on HP-UX to enable all library features
        CPPFLAGS="-D_POSIX_C_SOURCE=200112L $CPPFLAGS"
        AC_DEFINE(ABADOORAND_HAVE_HPUX, 1, [Have HPUX OS])
        LIBABADOORAND_CHECK_LANG_FLAG_PREPEND([-Ae])
        AC_CHECK_FUNCS(gethrtime)
        ;;
    *mingw32*)
        AC_DEFINE(ABADOORAND_HAVE_WINDOWS, 1, [Have Windows OS])
        AC_DEFINE(ABADOORAND_HAVE_MINGW32, 1, [Have MinGW32])
        AC_CHECK_HEADERS(windows.h)
        AC_CHECK_LIB(ws2_32, main, ,
            [AC_MSG_ERROR([cannot link with ws2_32.dll.])])
        AC_CHECK_LIB(rpcrt4, main, ,
            [AC_MSG_ERROR([cannot link with rpcrt4.dll.])])
        AC_CHECK_LIB(iphlpapi, main, ,
            [AC_MSG_ERROR([cannot link with iphlpapi.dll.])])
        # mingw32 defines __int64_t as long long
        AC_LANG_PUSH([C++])
        LIBABADOORAND_CHECK_LANG_FLAG_PREPEND([-Wno-long-long])
        AC_LANG_POP([C++])
        abadoorand_on_mingw32="yes"
        abadoorand_dso_visibility="no"

        if test "x$enable_static" = "xyes"; then
            AC_MSG_ERROR([Building static libraries is not supported under MinGW32])
        fi
        ;;
    *cygwin*)
        # Define on Cygwin to enable all library features
        CPPFLAGS="-D_GNU_SOURCE $CPPFLAGS"
        AC_DEFINE(ABADOORAND_HAVE_CYGWIN, 1, [Have Cygwin])
        if test "x$enable_static" = "xyes"; then
            AC_MSG_ERROR([Building static libraries is not supported under Cygwin])
        fi
        ;;
    *)
        AC_MSG_ERROR([unsupported system: ${host_os}.])
        ;;
esac

# Checks for libraries
AC_CHECK_LIB([pthread], [pthread_create])
AC_CHECK_LIB([rt], [clock_gettime])
AC_CHECK_LIB([sodium], [sodium_init],,AC_MSG_WARN(libsodium is needed for CURVE security))

#
# Check if the compiler supports -fvisibility=hidden flag. MinGW32 uses __declspec
#
if test "x$abadoorand_dso_visibility" = "xyes"; then
    AC_LANG_PUSH([C++])
    LIBABADOORAND_CHECK_LANG_VISIBILITY([LIBABADOORAND_EXTRA_CXXFLAGS="$abadoorand_cv_[]_AC_LANG_ABBREV[]_visibility_flag ${LIBABADOORAND_EXTRA_CXXFLAGS}"])
    AC_LANG_POP([C++])
fi

# CPU-specific optimizations
case "${host_cpu}" in
    *sparc*)
        AC_LANG_PUSH([C++])
        LIBABADOORAND_CHECK_LANG_FLAG_PREPEND([-mcpu=v9])
        AC_LANG_POP([C++])
    ;;
    *)
    ;;
esac

# Check whether to build docs / install man pages
LIBABADOORAND_CHECK_DOC_BUILD

# Check polling system
LIBABADOORAND_CHECK_POLLER([CPPFLAGS="${CPPFLAGS} -D${abadoorand_cv_poller_flag}"],
                    [AC_MSG_ERROR([Unable to continue without polling system])])

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(errno.h arpa/inet.h netinet/tcp.h netinet/in.h stddef.h \
stdlib.h string.h sys/socket.h sys/time.h time.h unistd.h limits.h)

# Check if we have ifaddrs.h header file.
AC_CHECK_HEADERS(ifaddrs.h, [AC_DEFINE(ABADOORAND_HAVE_IFADDRS, 1, [Have ifaddrs.h header.])])

# Check if we have sys/uio.h header file.
AC_CHECK_HEADERS(sys/uio.h, [AC_DEFINE(ABADOORAND_HAVE_UIO, 1, [Have uio.h header.])])

# Force not to use eventfd
AC_ARG_ENABLE([eventfd], [AS_HELP_STRING([--disable-eventfd], [disable eventfd [default=no]])],
    [abadoorand_disable_eventfd=yes], [abadoorand_disable_eventfd=no])

if test "x$abadoorand_disable_eventfd" != "xyes"; then
    # Check if we have eventfd.h header file.
    AC_CHECK_HEADERS(sys/eventfd.h,
                     [AC_DEFINE(ABADOORAND_HAVE_EVENTFD, 1, [Have eventfd extension.])])
fi

# Use c++ in subsequent tests
AC_LANG_PUSH(C++)

AC_CHECK_DECLS([SO_PEERCRED], [AC_DEFINE(ABADOORAND_HAVE_SO_PEERCRED, 1, [Have SO_PEERCRED socket option])], [], [#include <sys/socket.h>])
AC_CHECK_DECLS([LOCAL_PEERCRED], [AC_DEFINE(ABADOORAND_HAVE_LOCAL_PEERCRED, 1, [Have LOCAL_PEERCRED socket option])], [], [#include <sys/socket.h>])
AM_CONDITIONAL(HAVE_IPC_PEERCRED, test "x$ac_cv_have_decl_SO_PEERCRED" = "xyes" || test "x$ac_cv_have_decl_LOCAL_PEERCRED" = "xyes")

AC_HEADER_STDBOOL
AC_C_CONST
AC_C_INLINE
# Checks for typedefs, structures, and compiler characteristics.
if test "x$abadoorand_cv_[]_AC_LANG_ABBREV[]_intel_compiler" = "xyes"; then
    dnl 279: controlling expression is constant
    dnl Fixes build with ICC 12.x
    LIBABADOORAND_CHECK_WITH_FLAG([-wd279], [AC_TYPE_SIZE_T])
    LIBABADOORAND_CHECK_WITH_FLAG([-wd279], [AC_TYPE_SSIZE_T])
else
    AC_TYPE_SIZE_T
    AC_TYPE_SSIZE_T
fi
AC_HEADER_TIME
AC_TYPE_UINT32_T
AC_C_VOLATILE

#  PGM extension
abadoorand_pgm_ext="no"

pgm_basename="libpgm-5.2.122~dfsg"

AC_ARG_WITH([pgm], [AS_HELP_STRING([--with-pgm], 
    [build abadoorand with PGM extension [default=no]])], 
    [with_pgm_ext=$withval], [with_pgm_ext=no])

# build using system pgm
AC_ARG_WITH([system-pgm], [AS_HELP_STRING([--with-system-pgm],
    [build abadoorand with PGM extension. Requires pkg-config [default=no]])],
    [with_system_pgm_ext=yes], [with_system_pgm_ext=no])

if test "x$with_pgm_ext" != "xno" -a "x$with_system_pgm_ext" != "xno"; then
    AC_MSG_ERROR([--with-pgm and --with-system-pgm cannot be specified together])
fi

if test "x$with_pgm_ext" != "xno"; then

    # This allows placing the tar.gz to foreign/openpgm
    # and using ./configure --with-pgm=libpgm-x.y.z
    if test "x$with_pgm_ext" != "xyes"; then
        pgm_basename="$with_pgm_ext"
    fi

    # Unpack libpgm
    AC_MSG_NOTICE([Unpacking ${pgm_basename}.tar.gz])
    abadoorand_pwd=`pwd`
    cd foreign/openpgm

    if ! (gzip -dc "${pgm_basename}.tar.gz" || echo "failed") | ${am__untar}; then
        AC_MSG_ERROR([cannot unpack the foreign/openpgm/${pgm_basename}.tar.gz file])
    fi

    cd "${abadoorand_pwd}"

    if test -d foreign/openpgm/build-staging; then
        rm -rf foreign/openpgm/build-staging
    fi

    mv foreign/openpgm/${pgm_basename} foreign/openpgm/build-staging
    pgm_srcdir=foreign/openpgm/build-staging/openpgm/pgm

    if ! test -d foreign/openpgm/build-staging/openpgm/pgm/config; then
        mkdir foreign/openpgm/build-staging/openpgm/pgm/config
    fi

    # DSO symbol visibility for openpgm
    AC_LANG_PUSH([C])
    LIBABADOORAND_CHECK_LANG_VISIBILITY([ac_configure_args="CFLAGS='$abadoorand_cv_[]_AC_LANG_ABBREV[]_visibility_flag' ${ac_configure_args}"])
    AC_LANG_POP([C])

    pgm_subdir=build-staging/openpgm/pgm
    AC_SUBST(pgm_subdir)

    AC_SUBST(pgm_srcdir)
    AC_CONFIG_SUBDIRS([foreign/openpgm/build-staging/openpgm/pgm/])

    # Success!
    AC_DEFINE(ABADOORAND_HAVE_OPENPGM, 1, [Have OpenPGM extension])
    abadoorand_pgm_ext="yes"
fi

# Build with system openpgm
if test "x$with_system_pgm_ext" != "xno"; then
    m4_ifdef([PKG_CHECK_MODULES], [
        have_pgm_system_library="no"
        PKG_CHECK_MODULES([OpenPGM], [openpgm-5.2 >= 5.2],
            [ have_pgm_system_library="yes" ],
            [PKG_CHECK_MODULES([OpenPGM], [openpgm-5.1 >= 5.1],
                [ have_pgm_system_library="yes" ])
            ]
        )
        if test "x$have_pgm_system_library" = "xyes"; then
            AC_DEFINE(ABADOORAND_HAVE_OPENPGM, 1, [Have OpenPGM extension])
            LIBABADOORAND_EXTRA_CXXFLAGS="$OpenPGM_CFLAGS $LIBABADOORAND_EXTRA_CXXFLAGS"
            LIBS="$OpenPGM_LIBS $LIBS"
        fi
    ],
    [AC_MSG_ERROR([--with-system-pgm requires a working pkg-config installation])])
fi

AC_SUBST(pgm_basename)

# Set -Wall, -Werror and -pedantic
AC_LANG_PUSH([C++])

# Check how to enable -Wall
LIBABADOORAND_LANG_WALL([CPPFLAGS="$abadoorand_cv_[]_AC_LANG_ABBREV[]_wall_flag $CPPFLAGS"])

if test "x$abadoorand_werror" = "xyes" -a "x$abadoorand_cv_[]_AC_LANG_ABBREV[]_sun_studio_compiler" != "xyes"; then
    LIBABADOORAND_LANG_WERROR([CPPFLAGS="$abadoorand_cv_[]_AC_LANG_ABBREV[]_werror_flag $CPPFLAGS"])
fi

if test "x$abadoorand_pedantic" = "xyes"; then
    LIBABADOORAND_LANG_STRICT([CPPFLAGS="$abadoorand_cv_[]_AC_LANG_ABBREV[]_strict_flag $CPPFLAGS"])
fi
AC_LANG_POP([C++])

AM_CONDITIONAL(BUILD_TIPC, test "x$abadoorand_tipc_support" = "xyes")
AM_CONDITIONAL(BUILD_PGM, test "x$abadoorand_pgm_ext" = "xyes")
AM_CONDITIONAL(ON_MINGW, test "x$abadoorand_on_mingw32" = "xyes")
AM_CONDITIONAL(ON_ANDROID, test "x$abadoorand_on_android" = "xyes")
AM_CONDITIONAL(ON_LINUX, test "x$abadoorand_on_linux" = "xyes")

# Checks for library functions.
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(perror gettimeofday clock_gettime memset socket getifaddrs freeifaddrs fork)
AC_CHECK_HEADERS([alloca.h])
LIBABADOORAND_CHECK_SOCK_CLOEXEC([AC_DEFINE(
                              [ABADOORAND_HAVE_SOCK_CLOEXEC],
                              [1],
                              [Whether SOCK_CLOEXEC is defined and functioning.])
                          ])


# Subst LIBABADOORAND_EXTRA_CFLAGS & CXXFLAGS & LDFLAGS
AC_SUBST(LIBABADOORAND_EXTRA_CFLAGS)
AC_SUBST(LIBABADOORAND_EXTRA_CXXFLAGS)
AC_SUBST(LIBABADOORAND_EXTRA_LDFLAGS)

AC_CONFIG_FILES([Makefile \
    src/Makefile \
    src/abadoorand.pc \
    doc/Makefile \
    perf/Makefile \
    tests/Makefile \
    tools/Makefile \
    builds/msvc/Makefile \
    foreign/openpgm/Makefile \
    builds/redhat/abadoorand.spec])
AC_OUTPUT
